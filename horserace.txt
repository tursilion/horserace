
1 DIM N$(12),ODD(12),PLACE(12),STAT(12)
2 RANDOMIZE
5 CALL MAGNIFY(3)
10 CALL CLEAR
20 X1$="000000030303033F7FBFBF1F2040808000000000CC9C7AFFF0E0C08040201008"
30 X2$="000000030303033F7FBFBF1F1009050300000000CC9C7AFFF0E0C08080000000"
40 DISPLAY AT(12,6):"AT THE RACES"
41 OPEN #1:"DSK1.RECORD",INPUT
42 INPUT #1:HIGHEST,A$
43 CLOSE #1
45 DISPLAY AT(2,1):"BEST TOTAL:$";HIGHEST
46 DISPLAY AT(3,1):"BY ";A$
50 PRINT "PRESS ANY KEY TO BEGIN....."
60 CALL CHAR(96,X1$,100,X2$)
70 CALL SCREEN(15)
80 CALL SPRITE(#1,96,2,153,1,0,10)
85 CH=96
90 CALL KEY(0,K,S)
100 CALL PATTERN(#1,CH):: CH=-96*(CH=100)-100*(CH=96)
110 IF S=0 THEN 90
120 CALL DELSPRITE(ALL):: CALL CLEAR
130 MONEY=1000
140 FOR RACE=1 TO 9
150 NH=INT(RND*9)+4
155 CALL CLEAR :: CALL DELSPRITE(ALL)
160 FOR A=1 TO NH
165 CALL SPRITE(#A,96,A+1,(A-1)*16+1,160)
170 RESTORE :: FOR B=1 TO INT(RND*20)+1 :: READ N$(A):: NEXT B
171 FOR B=1 TO A-1 :: IF N$(A)=N$(B)THEN 170
172 NEXT B
173 DISPLAY AT(A*2,1):N$(A)
180 NEXT A
190 FOR A=1 TO 12 :: ODD(A)=0 :: NEXT A
200 FOR A=1 TO 100 :: B=INT(RND*12)+1 :: ODD(B)=ODD(B)+INT(RND*16):: NEXT A
210 FOR A=1 TO 12 :: ODD(A)=10-INT(ODD(A)/10):: IF ODD(A)<1 THEN ODD(A)=1
211 NEXT A
215 CALL CLEAR :: CALL DELSPRITE(ALL)
220 PRINT "RACE #";RACE
230 FOR A=1 TO NH
240 PRINT A;N$(A);TAB(22);ODD(A);": 1"
250 NEXT A
260 PRINT
270 PRINT "WHAT WOULD YOU LIKE TO BET?"
280 PRINT "(W)IN, (P)LACE, (S)HOW,"
290 PRINT "(E)XACTOR OR (Q)UINELLA."
300 PRINT "PRESS THE LETTER, THEN"
310 PRINT "PRESS ENTER.)"
320 ACCEPT SIZE(1)VALIDATE("WPSEQ"):B$
330 IF B$="" THEN 320
340 IF B$="E" THEN GOSUB 2000 ELSE IF B$="Q" THEN GOSUB 2500 ELSE GOSUB 1000
350 CALL CLEAR :: CALL VCHAR(1,30,42,24)
360 FOR A=1 TO NH :: CALL SPRITE(#A,96,A+1,(A-1)*16+1,1):: NEXT A
370 DISPLAY AT(12,7):"THERE THEY GO!";:: FOR A=1 TO 20 :: CALL SOUND(1,1500,0):: NEXT A :: CALL HCHAR(12,9,32,14)
380 FOR A=1 TO NH :: CALL MOTION(#A,0,1):: STAT(A)=0 :: NEXT A
390 CH=100
395 NM=1
400 REM
410 B=INT(RND*NH)+1 :: IF STAT(B)THEN 420
415 CALL MOTION(#B,0,INT(RND*5)+1)
420 FOR A=1 TO NH :: IF STAT(A)THEN 425
421 CALL PATTERN(#A,CH):: CALL POSITION(#A,R,C):: IF C>224 THEN CALL MOTION(#A,0,0):: STAT(A)=1 :: PLACE(NM)=A :: NM=NM+1
425 NEXT A
426 CH=-96*(CH=100)-100*(CH=96)
430 IF NM<=NH THEN 400
440 CALL CLEAR :: CALL DELSPRITE(ALL)
445 PRINT "OFFICIAL RESULTS"
447 PRINT :"PLACE","HORSE"
450 FOR A=1 TO NH
460 PRINT A;TAB(7);PLACE(A);TAB(12);N$(PLACE(A))
470 NEXT A
480 IF B$="E" THEN GOSUB 4000 ELSE IF B$="Q" THEN GOSUB 5000 ELSE GOSUB 3000
490 INPUT "PRESS ENTER->":A$
500 NEXT RACE
510 DATA SUNNY EASTER,WAY TO GO,HORSE,MISS CLEM,GIVE ME ACTION,I'M IN FRONT
520 DATA MYSTIC LIGHTNING,SUPERCHARGED,INDIAN PRINCESS,HOLD ON TIGHT
530 DATA SPEED DEMON,LITTLE DEVIL,GENESIS,ACER,MR ARNESON
540 DATA ON TOP,SLOWPOKE,CUREDENT,JUGGERNAUT,GUSTAFU
550 CALL CLEAR
560 PRINT "THAT'S ALL."
570 PRINT "YOU ENDED WITH $";MONEY
571 IF MONEY<HIGHEST THEN 580
572 IF MONEY=HIGHEST THEN PRINT "YOU TIED THE RECORD." :: GOTO 580
573 PRINT "YOU BROKE THE RECORD!"
574 INPUT "PLEASE ENTER YOUR NAME      ":A$
575 OPEN #1:"DSK1.RECORD",OUTPUT
576 PRINT #1:MONEY:SEG$(A$,1,10)
577 CLOSE #1
578 HIGHEST=MONEY
580 PRINT : :
590 PRINT "WOULD YOU LIKE TO TRY AGAIN?"
600 ACCEPT SIZE(1)VALIDATE("YN"):A$
610 IF A$="" THEN 600
620 IF A$="Y" THEN 120
999 END
1000 PRINT
1010 PRINT "YOU HAVE $";MONEY
1020 PRINT "HOW MUCH DO YOU BET?"
1030 INPUT BET
1035 BET=INT(BET)
1040 IF BET>MONEY THEN DISPLAY BEEP:"YOU DON'T HAVE THAT MUCH!" :: GOTO 1010
1050 MONEY=MONEY-BET
1060 PRINT "WHICH HORSE # ( 1 -";NH;")"
1070 INPUT HORSE
1080 IF (HORSE<1)+(HORSE>NH)+(HORSE<>INT(HORSE))THEN DISPLAY BEEP:"THAT NUMBER IS NOT RACING" :: GOTO 1060
1090 RETURN
2000 PRINT :"YOU HAVE $";MONEY
2010 PRINT "HOW MUCH DO YOU BET?"
2020 INPUT BET
2025 BET=INT(BET)
2030 IF BET>MONEY THEN DISPLAY BEEP:"YOU DON'T HAVE THAT MUCH!" :: GOTO 2000
2040 INPUT "FIRST PLACE:":HORSE1
2045 HORSE1=INT(HORSE1)
2050 IF (HORSE1<1)+(HORSE1>NH)THEN DISPLAY BEEP:"NOT RUNNING" :: GOTO 2040
2060 INPUT "SECOND PLACE:":HORSE2
2070 HORSE2=INT(HORSE2)
2080 IF (HORSE2<1)+(HORSE2>NH)THEN DISPLAY BEEP:"NOT RUNNING" :: GOTO 2060
2081 IF HORSE1=HORSE2 THEN DISPLAY BEEP:"PLEASE ENTER TWO DIFFERENT  NUMBERS." :: GOTO 2040
2085 MONEY=MONEY-BET
2090 RETURN
2500 PRINT
2510 PRINT "YOU HAVE $";MONEY
2520 INPUT "HOW MUCH DO YOU BET?":BET
2530 BET=INT(BET)
2540 IF (BET>MONEY)+(BET<1)THEN DISPLAY BEEP:"PLEASE BE REASONABLE." :: GOTO 2510
2550 INPUT "ENTER THE TWO HORSE NUMBERS SEPARATED BY A COMMA:":HORSE1,HORSE2
2560 HORSE1=INT(HORSE1):: HORSE2=INT(HORSE2)
2570 IF (HORSE1<1)+(HORSE1>NH)THEN DISPLAY BEEP:HORSE1;"NOT RUNNING" :: GOTO 2550
2580 IF (HORSE2<1)+(HORSE2>NH)THEN DISPLAY BEEP:HORSE2;"NOT RUNNING" :: GOTO 2550
2585 IF HORSE1=HORSE2 THEN DISPLAY BEEP:"PLEASE ENTER 2 DIFFERENT    NUMBERS" :: GOTO 2550
2587 MONEY=MONEY-BET
2590 RETURN
3000 ON -(B$="W")-2*(B$="P")-3*(B$="S")GOTO 3100,3200,3300
3100 IF PLACE(1)<>HORSE THEN 3600 ELSE 3400
3200 IF (PLACE(1)<>HORSE)*(PLACE(2)<>HORSE)THEN 3600 ELSE 3400
3300 IF (PLACE(1)<>HORSE)*(PLACE(2)<>HORSE)*(PLACE(3)<>HORSE)THEN 3600 ELSE 3400
3400 PRINT :"YOU WIN!"
3410 W=BET*ODD(HORSE)/(-(B$="W")-2*(B$="P")-3*(B$="S"))
3420 W=INT(W):: PRINT "YOU GET $";W
3430 MONEY=MONEY+W
3440 RETURN
3600 PRINT "YOU LOST, TOO BAD."
3610 IF MONEY>0 THEN RETURN
3620 PRINT "YOU HAVE NO MONEY LEFT."
3630 RACE=10
3640 RETURN
4000 IF PLACE(1)<>HORSE1 OR PLACE(2)<>HORSE2 THEN 3600
4010 PRINT "YOU WON THE QUINELLA!!!!!!!"
4020 W=BET*ODD(HORSE1)+BET*(ODD(HORSE2)
4030 PRINT "PAYOFF:$";W
4040 MONEY=MONEY+W
4050 RETURN
5000 IF ((PLACE(1)=HORSE1)+(PLACE(2)=HORSE1))*((PLACE(1)=HORSE2)+(PLACE(2)=HORSE2))THEN 5010
5005 GOTO 3600
5010 PRINT "YOU WIN THE QUINELLA!"
5020 W=INT((BET*ODD(HORSE1)+BET*ODD(HORSE2))/2)
5030 PRINT "WINNINGS:";W
5040 MONEY=MONEY+W
5050 RETURN
